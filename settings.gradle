pluginManagement {
    repositories {
        gradlePluginPortal() {
            content {
                [ 'com.google.code.gson', 'com.googlecode.javaewah', 'commons-codec', 'org.apache',
                  'org.apache.commons', 'org.eclipse.jgit', 'org.gradle.toolchains',
                  'org.gradle.toolchains.foojay-resolver-convention', 'org.junit', 'org.slf4j', 'org.sonatype.oss',
                  'org.zeroturnaround' ].forEach { includeGroup it }
            }
        }
        maven {
            name = 'Sinytra'
            url = uri('https://maven.sinytra.org/')
            content {
                [ 'org.sinytra.wiki', 'org.sinytra.wiki.toolkit' ].forEach { includeGroup(it) }
            }
        }
    }
}

plugins {
    id 'org.gradle.toolchains.foojay-resolver-convention' version '0.+'
}

rootProject.name = 'Music Triggers'

[ 'api', 'fabric', 'forge', 'legacy', 'shared',
  'legacy:1.12',
  'fabric:1.16',
  'forge:1.16',
  'shared:1.16', ].forEach { include it }

[ ':fabric:1.16',
  ':forge:1.16',
  ':legacy:1.12',
  ':shared:1.16' ].forEach {findProject(it)?.name = it.substring(1).replace('.','_').replace(':','.') }